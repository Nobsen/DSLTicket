grammar org.nordakademie.mwi.tickets.Tickets with org.eclipse.xtext.common.Terminals

generate tickets "http://www.nordakademie.org/mwi/tickets/Tickets"

TicketSystem:
	(states+=Status | flows+=Flow | fields+=Field | categories+=TicketCategory | roles+=Role)*;

Status:
	'status' name=QualifiedName description=STRING ';';

Flow:
	'flow' name=QualifiedName '{'
	(states+=[Status|QualifiedName] ';')* 
	'}';

Field:
	'field' name=QualifiedName label=STRING (fieldType=FieldType | fieldEnum=Enum) ';';

Enum:
	option+=STRING (',' option+=STRING)*;

TicketCategory:
	'ticketCategory' name=QualifiedName description=STRING '{'
		(flow=[Flow|QualifiedName] ';')?
		'fields {'
			(ticketFields+=TicketField ';')*
		'}'
	'}';

TicketField:
	field=[Field|QualifiedName] (mandatory?='mandatory')? (notOnList?='notOnList')?;

Role:
	'role' name=QualifiedName description=STRING '{'
	(rolepermissions+=RolePermission ';')*
	'}';

RolePermission:
	field=[Field|QualifiedName] permissions+=Permission (',' permissions+=Permission)*;

enum Permission:
	create | read | modify | delete;

// TODO user
enum FieldType:
	user | String | Date | dateTime | int | decimal;

QualifiedName:
	ID ('.' ID)*;
